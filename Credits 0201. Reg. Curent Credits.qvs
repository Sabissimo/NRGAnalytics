РегистрСведенийТаблицаТекущихКредитовPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_РегистрСведенийТаблицаТекущихКредитов.qvd]
(qvd);

РегистрСведенийТаблицаТекущихКредитовPreLoad:
LOAD 
	*,
    '00' as [ჰორიზონტალური შედეგი]
Resident РегистрСведенийТаблицаТекущихКредитовPre;

РегистрСведенийТаблицаТекущихКредитовPreLoad:
LOAD 
	*,
    '01' as [ჰორიზონტალური შედეგი]
Resident РегистрСведенийТаблицаТекущихКредитовPre;

drop table РегистрСведенийТаблицаТекущихКредитовPre;

РегистрСведенийТаблицаТекущихКредитовPre:
LOAD
	[სესხის დოკუმენტი] AS [%lnk_სესხის დოკუმენტი (მიმდინარე სესხები)], //Type: ДокументДоговорКредитаИЗайма,
	ApplyMap('MapДокументДоговорКредитаИЗаймаОрганизация',[სესხის დოკუმენტი],Null()) AS [%lnk_ორგანიზაცია (მიმდინარე სესხები)],
	ApplyMap('MapСправочникОрганизации',ApplyMap('MapДокументДоговорКредитаИЗаймаОрганизация',[სესხის დოკუმენტი],Null()),Null()) AS [ორგანიზაცია (მიმდინარე სესხები)],
	ApplyMap('MapДокументДоговорКредитаИЗаймаКонтрагент',[სესხის დოკუმენტი],Null()) AS [%lnk_კონტრაგენტი (მიმდინარე სესხები)],
	ApplyMap('MapСправочникКонтрагенты',ApplyMap('MapДокументДоговорКредитаИЗаймаКонтрагент',[სესხის დოკუმენტი],Null()),Null()) AS [კონტრაგენტი (მიმდინარე სესხები)],
	ApplyMap('MapДокументДоговорКредитаИЗаймаОвердрафт',[სესხის დოკუმენტი],Null()) AS [ოვერდრაფტი (მიმდინარე სესხები)],
	ApplyMap('MapДокументДоговорКредитаИЗаймаКредитнаяЛиния',[სესხის დოკუმენტი],Null()) AS [საკრედიტო ხაზი (მიმდინარე სესხები)],
	ApplyMap('MapДокументДоговорКредитаИЗаймаКредитнаяЛинияОвердрафт',[სესხის დოკუმენტი],Null()) AS [საკრედიტო ხაზი/ოვერდრაფტი (მიმდინარე სესხები)],
	ApplyMap('MapДокументДоговорКредитаИЗаймаПроцентнаяСтавка',[სესხის დოკუმენტი],0) AS [საპროცენტო განაკვეთი (მიმდინარე სესხები)],
    ApplyMap('MapДокументДоговорКредитаИЗаймаПроцентнаяСуммаДокумента',[სესხის დოკუმენტი],0) AS [დოკუმენტის თანხა (მიმდინარე სესხები)],    
	[ექვივალენტი] AS [ექვივალენტი (მიმდინარე სესხები)],
	[ხელშეკრულების თანხა] AS [ხელშეკრულების თანხა (მიმდინარე სესხები)],
	[ხელშეკრულების თანხა (ექვ.)] AS [ხელშეკრულების თანხა (ექვ.) (მიმდინარე სესხები)],
	[ხელშეკრულების თანხა (გამოსაყენებელი)] AS [ხელშეკრულების თანხა (გამოსაყენებელი) (მიმდინარე სესხები)],
	[ხელშეკრულების თანხა (გამოსაყენებელი, ექვ.)] AS [ხელშეკრულების თანხა (გამოსაყენებელი, ექვ.) (მიმდინარე სესხები)],
	[ძირის ნაშთი] AS [ძირის ნაშთი (მიმდინარე სესხები)],
	[ძირის ნაშთი (ექვ.)] AS [ძირის ნაშთი (ექვ.) (მიმდინარე სესხები)],
	[ვალუტა] AS [%lnk_ვალუტა (მიმდინარე სესხები)], //Type: СправочникВалюты,
	ApplyMap('MapСправочникВалюты',[ვალუტა],Null()) AS [ვალუტა (მიმდინარე სესხები)],
    if([ექვივალენტი] = '01', 99, if(ApplyMap('MapСправочникВалюты',[ვალუტა],Null()) = 'ლარი', 0, 1)) AS [ვერტიკალური პრიორიტეტი (მიმდინარე სესხები)],
    if([ჰორიზონტალური შედეგი] = '01', 99, [გრაფიკით]) as [ჰორიზონტალური პრიორიტეტი (მიმდინარე სესხები)],
    if([ექვივალენტი] = '01', 'Total', ApplyMap('MapСправочникВалюты',[ვალუტა],Null())) AS [ვალუტა (დაჯგუფება) (მიმდინარე სესხები)],
    if([ექვივალენტი] = '01', 1, ApplyMap('MapРегистрСведенийКурсыВалютСрезПоследних',[ვალუტა],Null())) AS [ვალუტის კურსი (მიმდინარე სესხები)],
	if([ჰორიზონტალური შედეგი] = '01', 'Total', [გრაფიკით]) AS [გრაფიკით (მიმდინარე სესხები)],
    if([ჰორიზონტალური შედეგი] = '01', 'Total', if([გრაფიკით]='01','გრაფიკიანი','უგრაფიკო')) AS [გრაფიკით (ტექსტით) (მიმდინარე სესხები)],
    [გრაფიკით] as [გრაფიკით (შედეგის გარეშე) (მიმდინარე სესხები)],
    [ჰორიზონტალური შედეგი] as [ჰორიზონტალური შედეგი (მიმდინარე სესხები)],
    if([გრაფიკით]='01','გრაფიკიანი',if(ApplyMap('MapДокументДоговорКредитаИЗаймаКредитнаяЛинияОвердрафт',[სესხის დოკუმენტი],Null())='01','საკრედიტო ხაზი/ოვერდრაფტი','პარტნიორების')) as [სახეობა (მიმდინარე სესხები)],
    if([გრაფიკით]='01','გრაფიკიანი','უგრაფიკო') AS [გრაფიკით (შედეგის გარეშე, ტექსტით) (მიმდინარე სესხები)],
    ApplyMap('MapСправочникВалюты',[ვალუტა],Null()) & ', ' & ApplyMap('MapСправочникОрганизации',ApplyMap('MapДокументДоговорКредитаИЗаймаОрганизация',[სესხის დოკუმენტი],Null()),Null()) as [ხაზის სახელი (მიმდინარე სესხები)],
    ApplyMap('MapСправочникКонтрагенты',ApplyMap('MapДокументДоговорКредитаИЗаймаКонтрагент',[სესხის დოკუმენტი],Null()),Null()) & ', ' & ApplyMap('MapСправочникОрганизации',ApplyMap('MapДокументДоговорКредитаИЗаймаОрганизация',[სესხის დოკუმენტი],Null()),Null())  & ', ' & [ხელშეკრულების თანხა] & ' ' & ApplyMap('MapСправочникВалюты',[ვალუტა],Null()) as [სესხის სახელი (მიმდინარე სესხები)]    
Resident РегистрСведенийТаблицаТекущихКредитовPreLoad;

Drop Table РегистрСведенийТаблицаТекущихКредитовPreLoad;

РегистрСведенийТаблицаТекущихКредитов:
Load
	*,
    [%lnk_ორგანიზაცია (მიმდინარე სესხები)]&[%lnk_კონტრაგენტი (მიმდინარე სესხები)]&[ოვერდრაფტი (მიმდინარე სესხები)]&[საკრედიტო ხაზი (მიმდინარე სესხები)]&[საკრედიტო ხაზი/ოვერდრაფტი (მიმდინარე სესხები)]&[სახეობა (მიმდინარე სესხები)] as [%lnk_სესხების იდენტიფიკატორი]
Resident РегистрСведенийТаблицаТекущихКредитовPre;

Drop Table РегистрСведенийТаблицаТекущихКредитовPre;