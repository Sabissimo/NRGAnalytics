LIB CONNECT TO 'Holding:Google_Drive_&_Spreadsheets - george.sabanadze@doctra.ge';

MapБулево:
Mapping 
Load * Inline 
[
lnk, YesNo
00, არა
01, კი
false, არა
true, კი
]; 

MapПланВидовХарактеристикДополнительныеРеквизитыИСведения:
Mapping LOAD
    [Ссылка],
    [Наименование];
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_ПланВидовХарактеристикДополнительныеРеквизитыИСведения.qvd]
(qvd);

СправочникКонтрагентыДополнительныеРеквизитыPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_СправочникКонтрагентыДополнительныеРеквизиты.qvd]
(qvd);

MapСправочникКонтрагентыДополнительныеРеквизитыInternal: 
Mapping LOAD
	[მინიშნება],
    ApplyMap('MapБулево',[მნიშვნელობა],'არა') as [მნიშვნელობა]
Resident СправочникКонтрагентыДополнительныеРеквизитыPre
Where
	ApplyMap('MapПланВидовХарактеристикДополнительныеРеквизитыИСведения',[თვისება],Null()) = 'Internal (2 ერთჯერადი)';

MapСправочникКонтрагентыДополнительныеРеквизитыМенеджерПродаж: 
Mapping LOAD
	[მინიშნება],
    ApplyMap('MapСправочникПользователи',[მნიშვნელობა],null()) as [მნიშვნელობა]
Resident СправочникКонтрагентыДополнительныеРеквизитыPre
Where
	ApplyMap('MapПланВидовХарактеристикДополнительныеРеквизитыИСведения',[თვისება],Null()) = 'გაყიდვების მენეჯ. (2 ერთჯერადი)';

drop table СправочникКонтрагентыДополнительныеРеквизитыPre;

СправочникДоговорыКонтрагентовPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_СправочникДоговорыКонтрагентов.qvd]
(qvd);

MapСрокОплатыПокупателя:
Mapping LOAD
    [მინიშნება],
    [მყიდველის გადახდის ვადა]
Resident СправочникДоговорыКонтрагентовPre;

Drop Table СправочникДоговорыКонтрагентовPre;

ДокументРасходнаяНакладнаяPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_ДокументРасходнаяНакладная-*.qvd]
(qvd);

MapДокументРасчетыСПокупателем:
Mapping LOAD
	[მინიშნება] AS [%lnk_მინიშნება], //Type: ДокументРасходнаяНакладная,
	Date(DayStart(AddYears([თარიღი],-2000)),'MM/DD/YYYY') AS [თარიღი]
Resident ДокументРасходнаяНакладнаяPre;

MapТипЗадолженностиДокумента:
Mapping LOAD
	[მინიშნება] AS [%lnk_მინიშნება (გასავლის ზედდებული,დოკუმენტი)], //Type: ДокументРасходнаяНакладная,
	ApplyMap('MapПеречислениеТипыЗадолженностей',[დავალიანების ტიპი],'არ არის შევსებული') AS [დავალიანების ტიპი (გასავლის ზედდებული,დოკუმენტი)]
Resident ДокументРасходнаяНакладнаяPre;

MapДокументРасходнаяНакладнаяСклад:
Mapping LOAD
	[მინიშნება],
	[საწყობი]
Resident ДокументРасходнаяНакладнаяPre;

Drop Table ДокументРасходнаяНакладнаяPre;

ДокументПередачаВАPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_ДокументПередачаВА-*.qvd]
(qvd);

MapДокументРасчетыСПокупателем:
Mapping LOAD
	[მინიშნება] AS [%lnk_მინიშნება], //Type: ДокументПередачаВА,
	Date(DayStart(AddYears([თარიღი],-2000)),'MM/DD/YYYY') AS [თარიღი]
Resident ДокументПередачаВАPre;

Drop Table ДокументПередачаВАPre;

РегистрСведенийЛимитыКонтрагентовСрезПоследнихPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_РегистрСведенийЛимитыКонтрагентовСрезПоследних-*.qvd]
(qvd);

MapЛимитыКонтрагентов:
Mapping LOAD
	[ხელშეკრულება] AS [%lnk_ხელშეკრულება (საკრედიტო ლიმიტები,ინფ. რეგისტრი)], //Type: СправочникДоговорыКонтрагентов,
	Sum([თანხა]) AS [თანხა (საკრედიტო ლიმიტები,ინფ. რეგისტრი)]	
Resident РегистрСведенийЛимитыКонтрагентовСрезПоследнихPre
// where 
//     match(ApplyMap('MapБулево',[აქტიური],Null()),'კი')
group by [ხელშეკრულება];

Drop Table РегистрСведенийЛимитыКонтрагентовСрезПоследнихPre;















 


MapНужныеНаправленияДеятельности:
MAPPING LOAD 
	[Product Direction] as [Product Direction], 
	1 as [Value];
SELECT 
	[Product Direction]
FROM GetWorksheetV2
WITH PROPERTIES (
worksheetKey='1khHxo_6tF2-fj0BWu77QLWC9ODRZOnw4DMteglpaYZc:_1450613259', //Qlik Matching, ActualDirections Sheet
gidOverride='',
generatedNumberedColumns='false',
skipRows=''
);

MapЛокация:
Mapping LOAD 
	[საწყობი] as [საწყობი], 
	[ლოკაცია] as [ლოკაცია];

SELECT [საწყობი],
	[ლოკაცია]
FROM GetWorksheetV2
WITH PROPERTIES (
worksheetKey='1-f15L80uaVKbMAD786Bp5NHDQlr7IqxFHhQoIBosNWA:_463252216',
gidOverride='',
generatedNumberedColumns='false',
skipRows=''
);

MapСправочникНаправленияДеятельности:
Mapping LOAD
    [მინიშნება],
    if(APPLYMAP('MapНужныеНаправленияДеятельности', [დასახელება])=1,[დასახელება],'სხვა');
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_СправочникНаправленияДеятельности.qvd]
(qvd);

СправочникСтруктурныеЕдиницыTemp:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/$(vSpacePrefix)_$(vPrefix)_СправочникСтруктурныеЕдиницы.qvd]
(qvd);

MapСправочникСтруктурныеЕдиницы:
Mapping LOAD
    [მინიშნება],
    [დასახელება]
Resident СправочникСтруктурныеЕдиницыTemp;

MapСправочникСтруктурныеЕдиницыКод:
Mapping LOAD
    [მინიშნება],
    [კოდი]
Resident СправочникСтруктурныеЕдиницыTemp;

Drop Table СправочникСтруктурныеЕдиницыTemp;