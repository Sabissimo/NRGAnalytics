AllDatesAccounting:
Load distinct
	[Date (BridgeTableOrgDate)] as %lnk_AccountingDate
Resident BridgeTableOrgDate;

StartAndEndDates:
Load
	MIN([%lnk_AccountingDate]) as MinDate,
    MAX([%lnk_AccountingDate]) as MaxDate
Resident AllDatesAccounting;

Drop Table AllDatesAccounting;

LET vMinDate = NUM(PEEK('MinDate',0,'StartAndEndDates'));
LET vMaxDate = If(NUM(PEEK('MaxDate', 0, 'StartAndEndDates')) < NUM(Now()), NUM(Now()), NUM(PEEK('MaxDate', 0, 'StartAndEndDates')));


drop tables StartAndEndDates;






TemporaryCalendar: 
LOAD 
	 $(vMinDate) + recno() - 1 AS TempDate 
AUTOGENERATE if(isnull(vMaxDate) or isnull(vMinDate),0,$(vMaxDate) - $(vMinDate) + 1);

AsOfTemporaryCalendarTotal:
LOAD DISTINCT
    (TempDate) AS [As Of TempDate]
RESIDENT TemporaryCalendar;

JOIN (AsOfTemporaryCalendarTotal)
LOAD DISTINCT
    TempDate
RESIDENT TemporaryCalendar;

AsOfTemporaryCalendar:
LOAD
	[As Of TempDate] as [TempDate],
    TempDate & '|' & 1 as [DateForConnect]
RESIDENT AsOfTemporaryCalendarTotal
WHERE (TempDate) <= [As Of TempDate];

DROP TABLE AsOfTemporaryCalendarTotal;

AsOfTemporaryCalendar:
LOAD
	TempDate as [TempDate],
    TempDate & '|' & 0 as [DateForConnect]
RESIDENT TemporaryCalendar;

AccountingCalendar:
Load	
    [TempDate]  as [TempDate],
    date([TempDate]) as [Date Accounting],
    YEAR([TempDate])*100 + MONTH([TempDate]) as [ym Accounting],
    //Year	([TempDate]) & floor (Month	([TempDate])) as [YM],
    'W'&Week	([TempDate])  	as [Week Accounting],
    Year	([TempDate])  	as [Year Accounting],
    Month	([TempDate])		as [Month Accounting],
    pick( match( num(month([TempDate])),1,2,3,4,5,6,7,8,9,10,11,12),
        dual('იან',1),
        dual('თებ',2),
        dual( 'მარ',3),
        dual('აპრ',4),
        dual('მაი',5),
        dual('ივნ',6),
        dual('ივლ',7),
        dual('აგვ',8),
        dual('სექ',9),
        dual('ოქტ',10),
        dual('ნოე',11),
        dual('დეკ',12)
    ) as [თვე Accounting],
    Day		([TempDate]) 		as [Day Accounting],
    Weekday ([TempDate]) 		as [Weekday Accounting],
    num(month([TempDate]))  as [MONTH_NUM Accounting],
    Year	([TempDate]) *10 + Ceil(Month([TempDate])/3)  as [yq Accounting],
    'Q' & Ceil(Month([TempDate])/3) as [Quarter Accounting],
    dual( Month([TempDate]) &'-'& right(Year([TempDate]),2) , Year([TempDate]) *100 + month([TempDate]) )  as [Year/Month Accounting],
    If([TempDate] < MonthStart(Today()) and [TempDate] >= MakeDate(2024,1,1), 1, 0) AS IncludeInForecastAccounting,
    If([TempDate] >= MakeDate(2024,2,1), 1, 0) AS ActualDataForAccounting
Resident TemporaryCalendar;

drop table TemporaryCalendar;