СправочникКонтрагентыPre:
LOAD
    *
FROM [lib://$(vSpace):DataFiles/Elvare_$(vPrefix)_СправочникКонтрагенты.qvd]
(qvd);

СправочникКонтрагентыИерархия:
Hierarchy([მინიშნება],  [ჯგუფი], [დასახელება])
LOAD      
	[მინიშნება],
	[ჯგუფი],
    [(არ გამოიყენება)] as [დასახელება] 
resident 
	СправочникКонтрагентыPre;

СправочникКонтрагенты:
LOAD
	[მინიშნება] AS [%lnk_მინიშნება (კონტრაგენტები)], //Type: СправочникКонтрагенты,
    [მინიშნება] AS [%lnk_კონტრაგენტი (ანგარიშსწორებები მყიდველებთან)], //Type: СправочникКонтрагенты,
    [კოდი] AS [კოდი (კონტრაგენტები)],
    [იურიდიული დასახელება] AS [კონტრაგენტი (ანგარიშსწორებები მყიდველებთან)],
	[იურიდიული დასახელება] AS [იურიდიული დასახელება (კონტრაგენტები)],
	[წაშლის ნიშნული] AS [წაშლის ნიშნული (კონტრაგენტები)],
	[ს.კ / პ.ნ] AS [ს.კ / პ.ნ (კონტრაგენტები)],
	[საკონტაქტო პირი] AS [%lnk_საკონტაქტო პირი (კონტრაგენტები,ცნობარი)], //Type: СправочникКонтактныеЛица,
	[ტელეფონის ნომერი] AS [ტელეფონის ნომერი (კონტრაგენტები,ცნობარი)],
	ApplyMap('MapПеречислениеВидыКонтрагентов',[კონტრაგენტის სახეობა],Null()) AS [კონტრაგენტის სახეობა (კონტრაგენტები)],
	ApplyMap('MapСправочникПользователи',[პასუხისმგებელი მომხმარებელი],Null()) AS [პასუხისმგებელი მომხმარებელი (კონტრაგენტები)],
	ApplyMap('MapСправочникСотрудники',[პასუხისმგებელი მენეჯერი],Null()) AS [პასუხისმგებელი მენეჯერი (კონტრაგენტები)],
	ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыInternal',[მინიშნება],'არა') AS [Internal (კონტრაგენტები)],
    ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыОбъединениеКонтрагентов',[მინიშნება],null()) AS [გაერთიანება (კონტრაგენტები)],
    ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыГородРегион',[მინიშნება],null()) AS [ქალაქი/რეგიონი (კონტრაგენტები)],
    ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыГодРегистрации',[მინიშნება],null()) AS [რეგისტრაციის წელი (კონტრაგენტები)],
//     ApplyMap('MapСправочникЗначенияСвойствОбъектовРодитель1', ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыЛокация',[მინიშნება],null()),null()) AS [ტერიტორია (კონტრაგენტები)],
//     ApplyMap('MapСправочникЗначенияСвойствОбъектовРодитель2', ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыЛокация',[მინიშნება],null()),null()) AS [რეგიონი (კონტრაგენტები)],
//     ApplyMap('MapСправочникЗначенияСвойствОбъектовРодитель3', ApplyMap('MapСправочникКонтрагентыДополнительныеРеквизитыЛокация',[მინიშნება],null()),null()) AS [ლოკაცია (კონტრაგენტები)],  
    [პასუხისმგებელი მომხმარებელი] AS [%lnk_პასუხისმგებელი მომხმარებელი (კონტრაგენტები)], //Type: СправочникПользователи,
	[პასუხისმგებელი მენეჯერი] AS [%lnk_პასუხისმგებელი მენეჯერი (კონტრაგენტები)], //Type: СправочникСотрудники,
	[კონტრაგენტის სახეობა] as [%lnk_კონტრაგენტის სახეობა (კონტრაგენტები)],
    [ჯგუფი] AS [%lnk_ჯგუფი (კონტრაგენტები)] //Type: СправочникКонтрагенты
Resident СправочникКонтрагентыPre;

Drop Table СправочникКонтрагентыPre;

left join(СправочникКонтрагенты)
Load
    [მინიშნება] as [%lnk_მინიშნება (კონტრაგენტები)],
    if(wildMatch([დასახელება1], '1. *', '2. *','3. *','4. *','5. *','6. *','7. *','8. *','9. *','10. *'),[დასახელება1],'5. ერთჯერადი') as [კონტაგენტის სეგმენტი (კონტრაგენტები)],
    if(wildMatch([დასახელება1], '1. *', '2. *','3. *','4. *','5. *','6. *','7. *','8. *','9. *','10. *'),[დასახელება2],'არ არის ჯგუფი') as [კონტაგენტის სეგმენტი L1 (კონტრაგენტები)],
    if(wildMatch([დასახელება1], '1. *', '2. *','3. *','4. *','5. *','6. *','7. *','8. *','9. *','10. *'),[დასახელება3],[დასახელება1]) as [კონტაგენტის სეგმენტი L2 (კონტრაგენტები)]
Resident 
	СправочникКонтрагентыИерархия;

Drop Table СправочникКонтрагентыИерархия;