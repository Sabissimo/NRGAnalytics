if(not IsPartialReload()) then

Let vNow = Floor(now());

ТипЗапасыTMP24:
LOAD * Inline [
'აღრიცხვის ანგარიშის ტიპიTemp'
'მარაგები'
];

РегистрНакопленияЗапасыTemp:
LOAD
    *
FROM [lib://$(vSpace24):DataFiles/$(vSpacePrefix24)_$(vPrefix24)_РегистрНакопленияЗапасы-*.qvd]
(qvd);

РегистрНакопленияЗапасыPre:
LOAD
    Date(DayStart(AddYears([Период],-2000)),'DD/MM/YYYY') as [პერიოდი],
	[სტრუქტურული ერთეული] as [სტრუქტურული ერთეული],
	[აღრიცხვის ანგარიში] as [აღრიცხვის ანგარიში],
	[ნომენკლატურა] as [ნომენკლატურა],
	[პარტია] as [პარტია],
	[მყიდველის შეკვეთა] as [მყიდველის შეკვეთა],
	[მახასიათებელი] as [მახასიათებელი],
	AutoNumberHash128(
		[სტრუქტურული ერთეული],
		[აღრიცხვის ანგარიში],
		[ნომენკლატურა],
		[პარტია],
		[მყიდველის შეკვეთა],
		[მახასიათებელი]
	) as [ნაშთების ჭრილი],
    round(Sum(If([ВидДвижения] = 1, -1, 1) * round([რაოდენობა],0.001)),0.001) AS [რაოდენობა],
	round(Sum(If([ВидДвижения] = 1, -1, 1) * round([თანხა],0.01)),0.01) AS [თანხა]    
Resident РегистрНакопленияЗапасыTemp
Where	
	floor(AddYears([Период],-2000)) <= $(vNow)
	and Exists([აღრიცხვის ანგარიშის ტიპიTemp],ApplyMap('MapПланСчетовУправленческийТипСчета',[აღრიცხვის ანგარიში],Null()))	
	and match([ორგანიზაცია],
    '85070A94EF044E4711EB87F29D540806', // elvare
    '80C6382C4ABBB43711E7363EBFB98EBD', // electric
    '8653D4F5EF3EE94F11EED6FFAF64F778' // elcom
    ) > 0
Group By 
	Date(DayStart(AddYears([Период],-2000)),'DD/MM/YYYY'),
	[სტრუქტურული ერთეული],
	[აღრიცხვის ანგარიში],
	[ნომენკლატურა],
	[პარტია],
	[მყიდველის შეკვეთა],
	[მახასიათებელი];


РегистрНакопленияЗапасыВысчетОстатковTemp:
LOAD
	RowNo() as [RowNo],
	[პერიოდი] as [პერიოდი],
	[სტრუქტურული ერთეული] as [სტრუქტურული ერთეული],
	[აღრიცხვის ანგარიში] as [აღრიცხვის ანგარიში],
	[ნომენკლატურა] as [ნომენკლატურა],
	[პარტია] as [პარტია],
	[მყიდველის შეკვეთა] as [მყიდველის შეკვეთა],
	[მახასიათებელი] as [მახასიათებელი],
	AutoNumberHash128(
		[სტრუქტურული ერთეული],
		[აღრიცხვის ანგარიში],
		[ნომენკლატურა],
		[პარტია],
		[მყიდველის შეკვეთა],
		[მახასიათებელი]
	) as [ნაშთების ჭრილი],
    round(if([ნაშთების ჭრილი]=peek([ნაშთების ჭრილი]), [რაოდენობა]+peek([რაოდენობა ნაშთი]), [რაოდენობა]),0.001) AS [რაოდენობა ნაშთი],
    round(if([ნაშთების ჭრილი]=peek([ნაშთების ჭრილი]), [თანხა]+peek([თანხა ნაშთი]), [თანხა]),0.01) AS [თანხა ნაშთი]
Resident РегистрНакопленияЗапасыPre
Order By
	[ნაშთების ჭრილი],
    [პერიოდი] asc;

Drop Table РегистрНакопленияЗапасыPre;

РегистрНакопленияЗапасыВысчетОстатков:
LOAD
	[პერიოდი] as [პერიოდი],
	[სტრუქტურული ერთეული] as [სტრუქტურული ერთეული],
	[აღრიცხვის ანგარიში] as [აღრიცხვის ანგარიში],
	[ნომენკლატურა] as [ნომენკლატურა],
	[პარტია] as [პარტია],
	[მყიდველის შეკვეთა] as [მყიდველის შეკვეთა],
	[მახასიათებელი] as [მახასიათებელი],
	[ნაშთების ჭრილი] as [ნაშთების ჭრილი],
    [რაოდენობა ნაშთი] as [რაოდენობა ნაშთი],
    [თანხა ნაშთი] AS [თანხა ნაშთი],
    [პერიოდი] as [ინტერვალის დაწყების თარიღი],
    if([ნაშთების ჭრილი]=peek([ნაშთების ჭრილი]), date(peek([პერიოდი])-1,'DD/MM/YYYY'), IF(Round([რაოდენობა ნაშთი],0.001) = 0 and Round([თანხა ნაშთი],0.01) = 0, [პერიოდი], $(vNow))) as [ინტერვალის დასრულების თარიღი],
    date([პერიოდი],'YYYY/MM/DD') & '|' & date(if([ნაშთების ჭრილი]=peek([ნაშთების ჭრილი]), date(peek([პერიოდი])-1,'DD/MM/YYYY'), IF(Round([რაოდენობა ნაშთი],0.001) = 0 and Round([თანხა ნაშთი],0.01) = 0, [პერიოდი], date($(vNow)))),'YYYY/MM/DD') as [ინტერვალი],
    AutoNumber(date([პერიოდი],'YYYY/MM/DD') & '|' & date(if([ნაშთების ჭრილი]=peek([ნაშთების ჭრილი]), date(peek([პერიოდი])-1,'DD/MM/YYYY'), IF(Round([რაოდენობა ნაშთი],0.001) = 0 and Round([თანხა ნაშთი],0.01) = 0, [პერიოდი], date($(vNow)))),'YYYY/MM/DD')) as [ინტერვალის იდენტიფიკატორი]
Resident РегистрНакопленияЗапасыВысчетОстатковTemp
Order By
	[RowNo] desc;
    
drop table РегистрНакопленияЗапасыВысчетОстатковTemp;    





////////////////////// Interval Match /////////////////////////////

Интервалы24:
LOAD Distinct
  [ინტერვალის დაწყების თარიღი] AS [ინტერვალის დაწყების თარიღი],
  [ინტერვალის დასრულების თარიღი] AS [ინტერვალის დასრულების თარიღი]
Resident РегистрНакопленияЗапасыВысчетОстатков;


ДатыНачалаИОкончанияИнтервалов24:
Load
	Min([ინტერვალის დაწყების თარიღი]) as MinDate,
    Max([ინტერვალის დასრულების თარიღი]) as MaxDate
Resident Интервалы24;

let vMinDate24 = peek('MinDate',0,'ДатыНачалаИОкончанияИнтервалов24') ;
let vMaxDate24 = NUM($(vNow));//peek('MaxDate',0,'ДатыНачалаИОкончанияИнтервалов24') ;

drop table ДатыНачалаИОкончанияИнтервалов24;

NoConcatenate
ДниИнтервалов24:
Load
	$(vMinDate24) + RowNo() - 1 as [ინტერვალის დღე]
AutoGenerate ($(vMaxDate24) - $(vMinDate24)+1);


Inner Join IntervalMatch ([ინტერვალის დღე])
Load Distinct 
  [ინტერვალის დაწყების თარიღი],
  [ინტერვალის დასრულების თარიღი]
Resident Интервалы24;

drop table Интервалы24;

Left Join(РегистрНакопленияЗапасыВысчетОстатков)
Load Distinct
  AutoNumber(date([ინტერვალის დაწყების თარიღი],'YYYY/MM/DD')&'|'&date([ინტერვალის დასრულების თარიღი],'YYYY/MM/DD')) as [ინტერვალის იდენტიფიკატორი],
  [ინტერვალის დღე] as [ინტერვალის დღე]
Resident ДниИнтервалов24;

drop table ДниИнтервалов24;





РегистрНакопленияЗапасы:
LOAD
	floor([ინტერვალის დღე]) AS StockDate,
    [ნომენკლატურა] & '|' & floor([ინტერვალის დღე]) & '|' & 0 as [ნომენკლატურა_პერიოდი_ნაშთია],
    0 as [ნაშთია (მარაგები)],
	[ინტერვალის დღე] AS [პერიოდი (მარაგები)],
    [სტრუქტურული ერთეული] AS [%lnk_სტრუქტურული ერთეული (მარაგები)], //Type: СправочникСтруктурныеЕдиницы,СправочникКонтрагенты,
	ApplyMap('MapСправочникСтруктурныеЕдиницы',[სტრუქტურული ერთეული],Null()) AS [სტრუქტურული ერთეული (მარაგები)],
	[აღრიცხვის ანგარიში] AS [%lnk_აღრიცხვის ანგარიში (მარაგები)], //Type: ПланСчетовУправленческий,
	ApplyMap('MapПланСчетовУправленческий',[აღრიცხვის ანგარიში],Null()) AS [აღრიცხვის ანგარიში (მარაგები)],
	[ნომენკლატურა] AS [%lnk_ნომენკლატურა (მარაგები)], //Type: СправочникНоменклатура,
	[პარტია] AS [%lnk_პარტია (მარაგები)], //Type: СправочникПартииНоменклатуры,
	ApplyMap('MapСправочникПартииНоменклатуры',[პარტია],Null()) AS [პარტია (მარაგები)],
	[მყიდველის შეკვეთა] AS [%lnk_მყიდველის შეკვეთა (მარაგები)], //Type: ДокументЗаказПокупателя,
	[მახასიათებელი] AS [%lnk_მახასიათებელი (მარაგები)], //Type: СправочникХарактеристикиНоменклатуры,
	ApplyMap('MapСправочникХарактеристикиНоменклатуры',[მახასიათებელი],Null()) AS [მახასიათებელი (მარაგები)],
	[რაოდენობა ნაშთი] AS [რაოდენობა (მარაგები, ნაშთი)],
    [თანხა ნაშთი] AS [თანხა (მარაგები, ნაშთი)]
Resident РегистрНакопленияЗапасыВысчетОстатков;
//where 
//	[ინტერვალის დღე] >= YearStart(YearStart($(vNow))-1);
    
Drop Table РегистрНакопленияЗапасыВысчетОстатков;












Concatenate(РегистрНакопленияЗапасы)
LOAD
	floor(Date(DayStart(AddYears([Период],-2000)),'DD/MM/YYYY')) AS StockDate,
	[ნომენკლატურა] & '|' & floor(Date(DayStart(AddYears([Период],-2000)),'DD/MM/YYYY')) & '|' & 0 as [ნომენკლატურა_პერიოდი_ნაშთია],
    0 as [ნაშთია (მარაგები)],
	Date(DayStart(AddYears([Период],-2000)),'DD/MM/YYYY') AS [პერიოდი (მარაგები)],
	[სტრუქტურული ერთეული] AS [%lnk_სტრუქტურული ერთეული (მარაგები)], //Type: СправочникСтруктурныеЕдиницы,СправочникКонтрагенты,
	ApplyMap('MapСправочникСтруктурныеЕдиницы',[სტრუქტურული ერთეული],Null()) AS [სტრუქტურული ერთეული (მარაგები)],
	[აღრიცხვის ანგარიში] AS [%lnk_აღრიცხვის ანგარიში (მარაგები)], //Type: ПланСчетовУправленческий,
	ApplyMap('MapПланСчетовУправленческий',[აღრიცხვის ანგარიში],Null()) AS [აღრიცხვის ანგარიში (მარაგები)],
	[ნომენკლატურა] AS [%lnk_ნომენკლატურა (მარაგები)], //Type: СправочникНоменклатура,
	[პარტია] AS [%lnk_პარტია (მარაგები)], //Type: СправочникПартииНоменклатуры,
	ApplyMap('MapСправочникПартииНоменклатуры',[პარტია],Null()) AS [პარტია (მარაგები)],
	[მყიდველის შეკვეთა] AS [%lnk_მყიდველის შეკვეთა (მარაგები)], //Type: ДокументЗаказПокупателя,
	[მახასიათებელი] AS [%lnk_მახასიათებელი (მარაგები)], //Type: СправочникХарактеристикиНоменклатуры,
	ApplyMap('MapСправочникХарактеристикиНоменклатуры',[მახასიათებელი],Null()) AS [მახასიათებელი (მარაგები)],
	[ВидДвижения] as [მოძრაობის ტიპი (მარაგები)],
    If([ВидДвижения] = 1, -1, 1) * [რაოდენობა] AS [რაოდენობა (მარაგები)],
	If([ВидДвижения] = 1, -1, 1) * [თანხა] AS [თანხა (მარაგები)] 
Resident РегистрНакопленияЗапасыTemp
Where	
	floor(AddYears([Период],-2000)) <= $(vNow)
	and Exists([აღრიცხვის ანგარიშის ტიპიTemp],ApplyMap('MapПланСчетовУправленческийТипСчета',[აღრიცხვის ანგარიში],Null()))	
	and match([ორგანიზაცია],
    '85070A94EF044E4711EB87F29D540806', // elvare
    '80C6382C4ABBB43711E7363EBFB98EBD', // electric
    '8653D4F5EF3EE94F11EED6FFAF64F778' // elcom
    ) > 0;

Drop Table ТипЗапасыTMP24;

Drop Table РегистрНакопленияЗапасыTemp;

End If